;=======================================================================================
; APPLinks.kxl
;=======================================================================================
; CreateINI($FileINI)
; CreateINIAPP($FileINI)
; CreateLinksGroup1 ($IniFile, $Section)
; CreateLinksGroup ($FileIni, $Domain, $Group)
; CreateLinks3 ($FileIni)
;=======================================================================================

   BREAK ON
   $SaveOption = SetOption ("WrapAtEOL", "ON")

;=======================================================================================
; Uses
;=======================================================================================

   ;=======================================================================================
   ;Global $Debug
   ;=======================================================================================
   Global $LogDir, $Log, $LogFile
   Global $FPrograms, $FTasks
   Global $sPrograms, $sTasks
   Global $SubDir, $SubDirTasks
   ;=======================================================================================
   Global $FUserAllGroups, $arrAllGroupsUser
   ;=======================================================================================
   Global $DomainUser, $DomainPC, $DomainAPP, $PCUser
   ;=======================================================================================

   ;=======================================================================================
   $FileINI_APP_DSP73   = $KxlDir+"\"+"apps_DSP73.ini"
   $FileINI_APP_REGION  = $KxlDir+"\"+"apps_REGION.ini"

   ;=======================================================================================
   $Shablon     = "CommandFile|Arg|WorkDir|IconFile|DestDir|SubDir|"
   ;=======================================================================================
   $FPrograms = "Программы"
   $SubDir = ""
   $sPrograms = $FPrograms+$SubDir
   ;=======================================================================================
   $FDocuments = "Документы"
   $SubDirDocuments = ""
   $sDocuments = $FDocuments+$SubDirDocuments
   ;=======================================================================================
   $FTasks = "Задачи"
   $SubDirTasks = ""
   $sTasks = $FTasks+$SubDirTasks
   ;=======================================================================================
   $LogDirectory = $LogPath
   ;=======================================================================================

;------------------------------------------------------
; TitleInfo
;------------------------------------------------------
function TitleInfo ($LLog, $LFile)
;begin
   ;--------------------------------------------------------------
   PrintGeneralTitle ($LLog, $LFile)
   ;--------------------------------------------------------------
   ;TitleInfoPC ()
   ;--------------------------------------------------------------
   ListDrives ($LLog, $LFile)
   ;--------------------------------------------------------------
   LogAdd ($LLog, $LFile, "I", "===========================================")
   LogAdd ($LLog, $LFile, "I", " Группы пользователя (глобальные)")
   LogAdd ($LLog, $LFile, "I", "===========================================")
   For Each $group In $arrAllGroupsUser
      LogAdd ($LLog, $LFile, "I", $group)
   next
   ;----------------------------------------------------------------------------------
EndFunction

;------------------------------------------------------
; TitleInfoPC
;------------------------------------------------------
function TitleInfoPC ()
;begin
   ;--------------------------------------------------------------
   $objWMIService = GetObject("winmgmts:"+"{impersonationLevel=impersonate}!\\" + @WKSTA + "\root\cimv2")
   $colCSItems = $objWMIService.ExecQuery("SELECT * FROM Win32_ComputerSystem")
   For Each $objCSItem In $colCSItems
      $Memory = $objCSItem.TotalPhysicalMemory
   Next
   $MemoryMB = VAL($Memory) / (1024 * 1024)
   $MHZ = ROUND (CDBL(@MHZ)/1000, 1) * 1000
   $File = FreeFileHandle

   $s = Replace(@ProductType, "/", "_", , )

   $FileName = $LogPath+"\"+AddCharR(" ",$DomainPC,6)+"_"+ AddCharR(" ",$PCUser,12)+" "+"_"+
               AddCharR(" ",$UBR,3)+" "+"_"+Trim(@CPU)+"_"+$MHZ+"_"+TRIM($s)+"_"+@CSD+"_"+$MemoryMB

   $FileName = $LogPath+"\"+$DomainPC+"\"+ AddCharR(" ",$PCUser,12)+" "+"_"+
               AddCharR(" ",$UBR,3)+" "+"_"+Trim(@CPU)+"_"+$MHZ+"_"+TRIM($s)+"_"+@CSD+"_"+$MemoryMB

   $DomainUser_01 = GET_DOMAIN_USER ($IPAddr)
   if @LDomain <> $DomainUser_01
      $FileName = $LogPath+"\"+AddCharR(" ","DSPESK",6)+"_"+ AddCharR(" ",$PCUser,12)+" "+"_"+
                  AddCharR(" ",$UBR,3)+" "+"_"+Trim(@CPU)+"_"+$MHZ+"_"+TRIM($s)+"_"+@CSD+"_"+$MemoryMB

      $FileName = $LogPath+"\"+"DSPESK"+"\"+ AddCharR(" ",$PCUser,12)+" "+"_"+
                  AddCharR(" ",$UBR,3)+" "+"_"+Trim(@CPU)+"_"+$MHZ+"_"+TRIM($s)+"_"+@CSD+"_"+$MemoryMB

   endif

   Del $FileName
   $Res = open($File, $FileName, 5)
   $Res = writeline($File,"")
   $Res = close($File)
   ;----------------------------------------------------------------------------------
EndFunction

;-------------------------------------------------
; LogonLog
;-------------------------------------------------
function LogonLog ($LDir)
;begin
   if Exist($LDir)
      $s = $USERID
      $LFile = LogFileName (1, $LDir, $s+".log")

      $sFIO = ""
      $sUBR = ""
      $sFIO = CheckUser73_Login_FIO ($Path_TOOLS+"\"+$UsersNetwork, $DomainUser, $USERID)
      $sUBR = CheckUser73_FIO_UBR ($Path_TOOLS+"\"+$Users73, $sFIO)
      if not Exist($LDir+"\"+$sUBR)
         MD $LDir+"\"+$sUBR
      endif

      $FileName = $LDir+"\"+$sUBR+"\"+$USERID+"."+$DomainUser
      $DomainUser_01 = GET_DOMAIN_USER ($IPAddr)
      if @LDomain <> $DomainUser_01
         $FileName = $LDir+"\"+$sUBR+"\"+$USERID+"."+"DSPESK"
      endif

      CreateTextFile($FileName)

      $s = Replace(@ProductType, "/", "_", , )
      LogAdd (1, $LFile, "P", "|"+$USERID+"|"+@FullName+"|"+
                                  @WKSTA+"|"+
                                  ;@ProductType+"|"+
                                  $s+"|"+
                                  $DomainUser+"|"+@LServer+"|"+$PCUser+"|"+$DomainPC+"|"+$sUBR+"|")
   endif
endfunction

;------------------------------------------------------
; CreateOne
;------------------------------------------------------
function CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, optional $WindowStyle, optional $PathAPP)
   Dim $s,$FileNamePif,$FileNameLnk
;begin
   $FileNamePif = $DestDirWork+"\"+$Name+".pif"
   $FileNameLnk = $DestDirWork+"\"+$Name+".lnk"
   if $Debug 
      $s = $DestDirWork+"\"+$Name+"|"+$CommandFile+"|"+$Arg+"|"+$WorkDir+"|"+$IconFile+"|"+$Name+"|"+$WindowStyle
      LogAdd($Log,$LogFile,"I", $s)
   endif
   $FileExt     = GetFileExt($CommandFile)

   ;----------------------------------------------------
   ; 1 вариант
   ;----------------------------------------------------
   ;$Minimize = 0
   ;$Replace = 0
   ;$RunInOwnSpace = 0
   ;$Result = CreateLink ($CommandFile, $Name, $IconFile, $IconIndex, $WorkDir, $Minimize, $Replace, $RunInOwnSpace)
   ;----------------------------------------------------

   ;----------------------------------------------------
   ; 2 вариант
   ;----------------------------------------------------
   ;$Result = CreateLinkLU ($KxlDir+"\Links.exe", $DestDirWork, $Name, $CommandFile, $Arg, $WorkDir, $IconFile,, $Name)
   ;----------------------------------------------------

   ;----------------------------------------------------
   ; 3 вариант
   ;----------------------------------------------------
   $Result = wshShortCut ($DestDirWork+"\"+$Name, $CommandFile, $Arg, $WorkDir, $IconFile, $WindowStyle, $Name)
   ;----------------------------------------------------
   if @Error <> 0
      LogAdd($Log,$LogFile,"I", "Ошибка при создании ("+$Name+"): "+@Error+" "+$SError) 
   else
      $ExeDir = $KxlDir+"\"
      ;----------------------------------------------------------------------
      ; PIF
      ;----------------------------------------------------------------------
      if Exist($FileNamePif)
         if Exist($PathAPP+"\"+$Name+".pif")
            LogAdd($Log,$LogFile,"I", $PathAPP+"\"+$Name+".pif"+" => "+$DestDirWork) 
            Copy $PathAPP+"\"+$Name+".pif" $DestDirWork
         else
            select
               case (@ProductType = "Windows 98") or (@ProductType = "Windows 95")
                  ;--------------------------------------------------------------
                  ; +0063h 0000h - default
                  ;        0010h - Закрывать окно по завершении работы
                  ;--------------------------------------------------------------
                  $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNamePif+'"'+" -o $63 -d $0010"
                  if $Debug 
                     LogAdd($Log,$LogFile,"I", $s) 
                  endif
                  shell "$s"
            EndSelect

            ;--------------------------------------------------------------
            ; 01ADh 00 02 10 02h - default
            ;                08h - полноэкранный режим
            ;          00        - обчный размер окна
            ;          10        - свернутое в значек
            ;          20        - развернутое во весь экран
            ;          80        - режим MS DOS        
            ;--------------------------------------------------------------
            if $WindowStyle = 3
               $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNamePif+'"'+" -o $1ad -d $00221008"
               if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               shell "$s"
            endif
         endif

      endif

      ;----------------------------------------------------------------------
      ; LNK
      ;----------------------------------------------------------------------
      if Exist($FileNameLnk)
         if Exist($PathAPP+"\"+$Name+".lnk")
            LogAdd($Log,$LogFile,"I", $PathAPP+"\"+$Name+".lnk"+" => "+$DestDirWork) 
            Copy $PathAPP+"\"+$Name+".lnk" $DestDirWork
         else
            if $WindowStyle = 3
               ;--------------------------------------------------------------
               ; Отображение во весь экран
               ;--------------------------------------------------------------
               if UCase($FileExt) = "EXE"
                  $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $3c -d $03"
                  if $Debug 
                     LogAdd($Log,$LogFile,"I", $s) 
                  endif
                  ; shell "$s"
               endif
               ;--------------------------------------------------------------
               ; Отображение во весь экран
               ;--------------------------------------------------------------
               if UCase($FileExt) = "BAT"
                  select
                     case $W2000
                        $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $234 -d $01"
                        shell "$s"
                     case $WXP or $WNT
                        $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $12d -d $1c"
                        shell "$s"
                  endselect
                  ;--------------------------------------------------------------
                  ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $131 -d $2d"
                  ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
                  ;shell "$s"
                  ;--------------------------------------------------------------
               endif

               ;--------------------------------------------------------------
               ; Размер буфера экрана (высота)
               ;--------------------------------------------------------------
               ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $23f -d $19"
               ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               ;shell "$s"
               ;--------------------------------------------------------------
               ; Размер окна (высота)
               ;--------------------------------------------------------------
               ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $243 -d $19"
               ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               ;shell "$s"
               ;--------------------------------------------------------------
            endif
         endif
      endif
   endif

   ;----------------------------------------------------
   $s = "      "
   if Exist($FileNamePif)
      ;$s = $s + $DestSubDir+"\"+GetFileName($FileNamePif)
      $s = $s + GetFileName($FileNamePif)
   else
      ;$s = $s + $DestSubDir+"\"+GetFileName($FileNameLnk)
      $s = $s + GetFileName($FileNameLnk)
   endif
   $s = $s + " ("+$WorkDir+")"
   if $Debug 
      LogAdd($Log,$LogFile,"I",$s)
   else
      LogAdd(10,  $LogFile,"I",$s)
   endif
   ;----------------------------------------------------
EndFunction

;------------------------------------------------------
; DeleteOne
;------------------------------------------------------
function DeleteOne ($Name, $DestDirWork, $DestSubDir)
   Dim $s,$FileNamePif,$FileNameLnk
;begin
   $FileNamePif = $DestDirWork+"\"+$Name+".pif"
   $FileNameLnk = $DestDirWork+"\"+$Name+".lnk"
   $s = "Delete link ... "
   if Exist($FileNamePif)
      $s = $s + $DestSubDir+"\"+GetFileName($FileNamePif)
      Del "$FileNamePif"
      LogAdd($Log,$LogFile,"I",$s)
   endif
   if Exist($FileNameLnk)
      $s = $s + $DestSubDir+"\"+GetFileName($FileNameLnk)
      Del "$FileNameLnk"
      LogAdd($Log,$LogFile,"I",$s)
   endif
EndFunction

;------------------------------------------------------
; GetDirWork
;------------------------------------------------------
function GetDirWork ($Dir, $SubDirAPP, optional $Server)
;begin
   select 
      case UCase($Dir) = UCase($FPrograms)
         if $Server
            $GetDirWork  = $Desktop+"\"+$sPrograms+"_"+$Server
         else
            $GetDirWork  = $Desktop+"\"+$sPrograms
         endif
      case UCase($Dir) = UCase($FDocuments)
         if $Server
            $GetDirWork  = $Desktop+"\"+$sDocuments+"_"+$Server
         else
            $GetDirWork  = $MyDocuments+"\"+$sDocuments
            $GetDirWork  = $Desktop+"\"+$sDocuments
         endif
      case UCase($Dir) = UCase($FTasks)
         if $Server
            $GetDirWork  = $Desktop+"\"+$sTasks+"_"+$Server
         else
            $GetDirWork  = $Desktop+"\"+$sTasks
         endif
      case UCase($Dir) = "DESKTOP"
         $GetDirWork  = $Desktop
      case UCase($Dir) = "PROGRAMS"
         $GetDirWork  = $Programs
      case UCase($Dir) = "STARTMENU"
         $GetDirWork  = $StartMenu
      case 1
         if Exist($Dir)
            $GetDirWork = $Dir
         else
            if $Server
               $GetDirWork  = $Desktop+"\"+$sPrograms+"_"+$Server
            else
               $GetDirWork  = $Desktop+"\"+$sPrograms
            endif
         endif
   endselect

   if $SubDirAPP <> ""
      $GetDirWork  = $GetDirWork+"\"+$SubDirAPP
   endif

EndFunction

;------------------------------------------------------
; CreateLinksGroup
;------------------------------------------------------
function CreateLinksGroup ($FileIni, $Group, $APP, optional $Server)
   Dim $Commands, $Command, $Server_APP
   Dim $s, $i
   Dim $Name
   Dim $DestDir, $DestDirWork, $SubDirAPP, $DestSubDir
   Dim $IconIndex
   Dim $Minimize, $Replace, $RunInOwnSpace
;begin 
   $Server_APP = ExtractWord(2, $APP, "_")

   $Commands = ReadSection($FileIni, $Group)
   $PathAPP  = GetFilePath($FileIni)
   $i = 0
   for each $Command in $Commands
      $i = $i + 1
      $Name = $Command
      $s = ReadString ($FileIni, $Group, $Name, "")+"|"

      if $Debug 
         LogAdd($Log,$LogFile,"I", $Group+"="+$s) 
      endif

      $CommandFile   = ExtractWord(1, $s, "|")
      $Arg           = ExtractWord(2, $s, "|")
      $WorkDir       = ExtractWord(3, $s, "|")
      $IconFile      = ExtractWord(4, $s, "|")
      $IconIndex     = 0
      $DestDir       = ExtractWord(5, $s, "|") 
      $SubDirAPP     = ExtractWord(6, $s, "|")
      $WindowStyle   = ExtractWord(7, $s, "|")

      $DestDirWork   = GetDirWork($DestDir, $SubDirAPP, $Server)
      $DestSubDir    = $DestDirWork

      if $Debug 
         LogAdd($Log,$LogFile,"I", $DestDir+"|"+$SubDirAPP+"|"+$DestDirWork)
      endif

      if Exist ($CommandFile)
         ;----------------------------------------------------------------
         if $Debug 
            LogAdd($Log,$LogFile,"I", "   "+$Name)
         endif
         ;----------------------------------------------------------------
         if (UCase($DestDir) = UCase($FPrograms)) and (not IsTerminalApplication)
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         else
         ;----------------------------------------------------------------
         if (UCase($DestDir) = UCase($FTasks)) and (IsTerminalApplication)
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         else
         ;----------------------------------------------------------------
         if (UCase($DestDir) <> UCase($FPrograms)) and (UCase($DestDir) <> UCase($FTasks))
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         endif
         ;----------------------------------------------------------------
         endif
         endif
         ;----------------------------------------------------------------

      else
         if $CommandFile <> ""
            if UCase($APP) <> UCase("Terminal")
               LogAdd($Log,$LogFile,"I", "ERROR: "+$Name+" Файл "+$CommandFile+" не существует или нет доступа")
            endif
         endif

         ; Delete Lnk

         if UCase($DestDir) = UCase($FPrograms) and (not IsTerminalApplication)
            if $SubDirAPP <> ""
               $DestSubDir =  $sPrograms+"\"+$SubDirAPP
            else
               $DestSubDir =  $sPrograms
            endif
            DeleteOne ($Name, $DestDirWork, $DestSubDir)
         else

         if UCase($DestDir) = UCase($FTasks) and (IsTerminalApplication)
            if $SubDirAPP <> ""
               $DestSubDir =  $sTasks+"\"+$SubDirAPP
            else
               $DestSubDir =  $sTasks
            endif
            DeleteOne ($Name, $DestDirWork, $DestSubDir)
         endif

         endif
      endif
   next
EndFunction

;------------------------------------------------------
; CreateLinksAPP
;------------------------------------------------------
function CreateLinksAPP ($FileIni, $Domain, $Group, optional $Server)
   Dim $FileINIApp, $LGroup, $i, $j, $n, $Done, $Object, $Objects, $s, $LGroups, $Item
;begin

   $FileINIApp = ReadString ($FileINI, $Domain, $Group, "")

   if Exist($FileINIApp) 

      if $Debug
         LogAdd($Log,$LogFile,"I","Файл "+$FileINIApp+" существует")
      endif

      $Objects = ReadSections($FileINIAPP)
      for each $Object in $Objects
         $j = 0
         if $Object <> ""
            if $Debug
               LogAdd($Log,$LogFile,"I",$Object)
            endif

            $n = WordCount($Object, ",")
            if $n > 0

               ;------------------------------------------------------
               ; реализация ИЛИ
               ;------------------------------------------------------
               $Done = 0
               $i = 1

               while ($i <= $n) and ($Done = 0)
                  $Item = ExtractWord($i, $Object, ",")

                  if $Item 

                     ;------------------------------------------
                     ; User без домена
                     ;------------------------------------------
                     if WordCount($Item, "\") = 1
                        $Item = $DomainUser+"\"+$Item+"\"
                     endif

                     $LDomen = ExtractWord(1, $Item, "\")
                     $s = ExtractWord(2, $Item, "\")

                     ;------------------------------------------
                     ; User
                     ;------------------------------------------
                     if WordCount($Item, "\") = 3
                        if $Debug
                           LogAdd($Log,$LogFile,"I","USER="+$Item)
                           LogAdd($Log,$LogFile,"I",UCase($LDomen+"\"+$s)+"="+UCase($DomainUser+"\"+$UserID))
                        endif

                        ;------------------------------------------------
                        ;if UCase($s) = UCase($UserID)
                        ;   $Done = 1
                        ;endif
                        ;------------------------------------------------

                        if UCase($LDomen+"\"+$s) = UCase($DomainUser+"\"+$UserID)
                           $Done = 1
                        endif
                        if UCase($LDomen+"\"+$s) = UCase($DomainUser+"\"+"all")
                           $Done = 1
                        endif

                     else
                        if $Debug
                           LogAdd($Log,$LogFile,"I","GROUP="+$Item)
                        endif
                        ;------------------------------------------
                        ; GROUP
                        ;------------------------------------------

                        if $Debug
                           $NumberItem1 = IndexOf($arrAllGroupsUser, $Item)
                           $NumberItem2 = IndexOf($FUserAllGroups, $Item)
                           LogAdd($Log,$LogFile,"I","***********/"+$NumberItem1+"/"+$NumberItem2+"/"+$Item)
                        endif
                        
                        If (IndexOf($arrAllGroupsUser, $Item) >= 0)

                           if $Debug
                              LogAdd($Log,$LogFile,"I","*  "+$Item)
                           endif
                           $Done = 1
                        else
                           ;-----------------------------------------------------
                           ;if IndexOf($FUserAllGroups, $Item) >= 0
                           ;-----------------------------------------------------
                           if IndexOf($arrAllGroupsUser, $Item) >= 0
                              if $Debug
                                 LogAdd($Log,$LogFile,"I","** "+$Item)
                              endif
                              $Done = 1
                           endif
                           if $Debug
                              LogAdd($Log,$LogFile,"I","************** "+$Item)
                           endif
                        endif

                     endif
                  endif
                  $i = $i + 1
               loop

               ;------------------------------------------------------
               if $Done = 1
                  $DomainAPP = ExtractWord(1, $Item, "\")
                  if $Debug
                     LogAdd($Log,$LogFile,"I","DomainAPP="+UCase($DomainAPP)+" "+
                                              "DomainUser="+UCase($DomainUser)+" "+
                                              "DomainPC="+UCase($DomainPC))
                  endif
                  select
                     case UCase($DomainAPP) = UCase($DomainDSP)
                        if (UCase($DomainUser) = UCase($DomainDSP)) and (UCase($DomainPC) = UCase($DomainDSP))
                           $j = $j + 1         
                           CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                        else
                        ;-------------------------------------------------------------
                        if (UCase($DomainUser) = UCase($DomainDSP)) and (UCase($DomainPC) <> UCase($DomainDSP))
                           $j = $j + 1         
                           CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                        endif
                        ;-------------------------------------------------------------
                        endif
                     case 1
                        $j = $j + 1         
                        CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                  endselect
               endif
               ;------------------------------------------------------

            endif
         endif
      next
   else
      if $Debug
         LogAdd($Log,$LogFile,"I","Файл "+$FileINIApp+" не существует или нет доступа")
      endif
   endif
EndFunction

;------------------------------------------------------
; CreateLinks0
;------------------------------------------------------
function CreateLinks0 ($FileIni)
   Dim $Domains, $Groups, $Group, $Domain, $i, $DomainGroup
;begin 
   $Groups = AllGroups(0)
   for each $DomainGroup in $Groups
      $i = WordCount($DomainGroup, "\")
      if $i > 1
         $Domain = ExtractWord(1, $DomainGroup, "\")
         $Group = ExtractWord(2, $DomainGroup, "\")
         if ValueExists ($FileIni, $Domain, $Group) = True
            $DomainAPP = $Domain
            CreateLinksAPP ($FileIni, $Domain, $Group)
         endif
      endif
   next
EndFunction

;------------------------------------------------------
; CreateLinks3
;------------------------------------------------------
function CreateLinks3 ($FileIni, optional $Server)
   Dim $Domains, $APPs, $APP
;begin 
   LogAdd ($Log, $LogFile, "I", "========================================")
   LogAdd ($Log, $LogFile, "I", "FileINI_APP = " + $FileIni)
   $Domains = ReadSections($FileINI)
   for each $Domain in $Domains
      $DomainAPP = $Domain
      if $Domain <> ""
         if $Debug 
            LogAdd($Log,$LogFile,"I","=====================================")
            LogAdd($Log,$LogFile,"I","Домен: "+$DomainAPP)
         endif
         $APPs = ReadSection($FileINI, $Domain)
         for each $APP in $APPs
            if $APP <> ""
               if $Debug 
                  LogAdd($Log,$LogFile,"I","-------------------------------------")
                  LogAdd($Log,$LogFile,"I","Программа: "+$APP)
               endif
               CreateLinksAPP ($FileIni, $Domain, $APP, $Server)
            endif
         next
      endif
   next
   LogAdd ($Log, $LogFile, "I", "========================================")
EndFunction

;------------------------------------------------------
; ClearDir
;------------------------------------------------------
function ClearDir ($Dir, optional $Server)
   Dim $s
;begin
   $s = GetDirWork($Dir, "", $Server)
   if $Debug 
      LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
   endif
   DelDir ($s, $Log, $LogFile)
   RD ($s)

   ;----------------------------------------------------------
   ; Start Menu
   ;----------------------------------------------------------
   select
      case UCase($Dir) = UCase($FPrograms)
         if $Server
            $s = $Programs+"\"+$sPrograms+"_"+$Server
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         else
            $s = $Programs+"\"+$sPrograms
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         endif
      case UCase($Dir) = UCase($FDocuments)
         if $Server
            $s = $Programs+"\"+$sDocuments+"_"+$Server
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         else
            $s = $Programs+"\"+$sDocuments
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         endif
      case UCase($Dir) = UCase($FTasks)
         if $Server
            $s = $Programs+"\"+$sTasks+"_"+$Server
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir($s, $Log, $LogFile)
         else
            $s = $Programs+"\"+$sTasks
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir($s, $Log, $LogFile)
         endif
   endselect
EndFunction

;------------------------------------------------------
; CreateLinks
;------------------------------------------------------
function CreateLinks
;begin
   ;------------------------------------------------------
   $DomainUser = @LDomain
   $DomainPC   = @Domain
   $PCUser     = %COMPUTERNAME%
   $IPAddr = ExtractWord(1,GetIP,"/")
   if $PCUser = "" $PCUser = @Wksta endif
   ;------------------------------------------------------

   $Log = 30 
   if $DomainUser = ""
      $LogDir = $LogPath+"\logon"+"\lnk"
   else
      $LogDir = $LogPath+"\logon\"+$DomainUser+"\lnk"
   endif
   if not Exist($LogDir) MD $LogDir endif
   if $DomainUser = "REGION"
      $s = AddCharR("_",$USERID,15)
   else
   if $DomainUser = "DSP73"
      $s = AddCharR("_",$USERID,15)
   else
      $s = AddCharR("_",$USERID,8)
   endif
   endif

   $LogFile = $s+"_"+UCase(@WKSTA)+".lnk"
   $LogFile = LogFileName($Log,$LogDir,$LogFile)
   ;------------------------------------------------------
   if $Debug
      LogAdd ($Log, $LogFile, "I", ":==================================================")
      LogAdd ($Log, $LogFile, "I", ": Режим отладки включен.")
      LogAdd ($Log, $LogFile, "I", ":==================================================")
   endif
   ;------------------------------------------------------
   $FUserAllGroups   = AllGroups(0)
   $arrAllGroupsUser = GetAllGroupsUser(0)
   ;------------------------------------------------------

   ;------------------------------------------------------
   if $Debug
      TitleInfo (30, $LogFile)
   else
      TitleInfo (10, $LogFile)
   endif

   ;------------------------------------------------------
   ; Backup Kix32 -> %WINDIR%
   ;------------------------------------------------------
   $SystemDir = %WINDIR%
   $KixDirKix = $KxlDir+"\Kix"
   BacFile ($KixDirKix, $SystemDir, "kix32.exe", True)
   ;------------------------------------

   LogAdd ($Log, $LogFile, "I", "===========================================")
   LogAdd ($Log, $LogFile, "I", " Создание ярлыков ")
   LogAdd ($Log, $LogFile, "I", "===========================================")

   ClearDir($FPrograms)
   ClearDir($FDocuments)

   select
      case Ucase ("%USERDOMAIN%") = "REGION"
         CreateLinks3 ($FileINI_APP_REGION)
         $DomainUser_01 = GET_DOMAIN_USER ($IPAddr)

         if $DomainUser_01 = "DSP73"
            CreateLinks3 ($FileINI_APP_DSP73)
         endif

      case Ucase ("%USERDOMAIN%") = "DSP73"
         CreateLinks3 ($FileINI_APP_REGION)
         CreateLinks3 ($FileINI_APP_DSP73)
   endselect

   $DestDirWork1 = GetDirWork($sPrograms, "")
   $DestDirWork2 = $Programs+"\"+$sPrograms
   if $Debug 
      LogAdd($Log,$LogFile,"I", $DestDirWork1+" => "+$DestDirWork2)
   endif
   BacFiles ($DestDirWork1, $DestDirWork2, "*.*", True)
endfunction
