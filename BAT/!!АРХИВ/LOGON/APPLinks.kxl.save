;=======================================================================================
; APPLinks.kxl
;=======================================================================================
; CreateINI($FileINI)
; CreateINIAPP($FileINI)
; CreateLinksGroup1 ($IniFile, $Section)
; CreateLinksGroup ($FileIni, $Domain, $Group)
; CreateLinks3 ($FileIni)
;=======================================================================================

   BREAK ON
   $SaveOption = SetOption ("WrapAtEOL", "ON")

;=======================================================================================
; Uses
;=======================================================================================

   ;=======================================================================================
   ;Global $Debug
   ;=======================================================================================
   Global $LogDir, $Log, $LogFile
   Global $FPrograms, $FTasks
   Global $sPrograms, $sTasks
   Global $SubDir, $SubDirTasks
   ;=======================================================================================
   Global $FUserAllGroups, $arrAllGroupsUser
   ;=======================================================================================
   Global $DomainUser, $DomainPC, $DomainAPP, $PCUser
   ;=======================================================================================

   ;=======================================================================================
   $FileINI_APP_GU      = $KxlDir+"\"+"apps.ini"
   $FileINI_APP_DSP     = $KxlDir+"\"+"apps.ini"
   $FileINI_APP_ULNSK   = $KxlDir+"\"+"apps.ini"
   $FileINI_APP_REGION  = $KxlDir+"\"+"apps_REGION.ini"
   $FileINI_APP_S73FS01 = $KxlDir+"\"+"apps_S73FS01.ini"

   ;=======================================================================================
   $Shablon     = "CommandFile|Arg|WorkDir|IconFile|DestDir|SubDir|"
   $FPrograms = "Программы"
   $SubDir = "\TEST"
   $SubDir = ""
   $sPrograms = $FPrograms+$SubDir
   $FTasks = "Задачи"
   $SubDirTasks = "\TEST"
   $SubDirTasks = ""
   $sTasks = $FTasks+$SubDirTasks

   ;=======================================================================================
   $LogDirectory = $LogPath

;------------------------------------------------------
; TitleInfo
;------------------------------------------------------
function TitleInfo ($LLog, $LFile)
;begin
   PrintGeneralTitle ($LLog, $LFile)

   if $PCUser <> "APPS2"
      $File = FreeFileHandle
      $FileName = $LogDirectory+"\"+AddCharR(" ",$PCUser,12)+" "+"("+Trim(@CPU)+")"+"."+AddCharR(" ",@ProductType,25)
      Del $FileName
      $Res = open($File, $FileName, 5)
      $Res = writeline($File,"")
      $Res = close($File)
   endif

   ListDrives ($LLog, $LFile)

   ;-------------------------------------------------------------------------------
   ;LogAdd ($LLog, $LFile, "I", "===========================================")
   ;LogAdd ($LLog, $LFile, "I", " Группы пользователя (все)")
   ;LogAdd ($LLog, $LFile, "I", "===========================================")
   ;For Each $group In $FUserAllGroups
   ;   LogAdd ($LLog, $LFile, "I", $group)
   ;next
   ;-------------------------------------------------------------------------------

   LogAdd ($LLog, $LFile, "I", "===========================================")
   LogAdd ($LLog, $LFile, "I", " Группы пользователя (глобальные)")
   LogAdd ($LLog, $LFile, "I", "===========================================")
   For Each $group In $arrAllGroupsUser
      LogAdd ($LLog, $LFile, "I", $group)
   next
   ;----------------------------------------------------------------------------------
EndFunction

;-------------------------------------------------
; LogonLog
;-------------------------------------------------
function LogonLog ($LDir)
;begin
   if Exist($LDir)
      $s = $USERID
      if (IsTerminalApplication)
         $LFile = LogFileName (1, $LDir, $s+".log")
      else
         $LFile = LogFileName (1, $LDir, $s+".log")
      endif
      LogAdd (1, $LFile, "P", "|"+$USERID+"|"+@FullName+"|"+
                                  @WKSTA+"|"+@ProductType+"|"+
                                  $DomainUser+"|"+@LServer+"|"+$PCUser+"|"+$DomainPC)
   endif
endfunction

;------------------------------------------------------
; CreateOne
;------------------------------------------------------
function CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, optional $WindowStyle, optional $PathAPP)
   Dim $s,$FileNamePif,$FileNameLnk
;begin
   $FileNamePif = $DestDirWork+"\"+$Name+".pif"
   $FileNameLnk = $DestDirWork+"\"+$Name+".lnk"
   if $Debug 
      $s = $DestDirWork+"\"+$Name+"|"+$CommandFile+"|"+$Arg+"|"+$WorkDir+"|"+$IconFile+"|"+$Name+"|"+$WindowStyle
      LogAdd($Log,$LogFile,"I", $s)
   endif
   $FileExt     = GetFileExt($CommandFile)

   ;----------------------------------------------------
   ; 1 вариант
   ;----------------------------------------------------
   ;$Minimize = 0
   ;$Replace = 0
   ;$RunInOwnSpace = 0
   ;$Result = CreateLink ($CommandFile, $Name, $IconFile, $IconIndex, $WorkDir, $Minimize, $Replace, $RunInOwnSpace)
   ;----------------------------------------------------

   ;----------------------------------------------------
   ; 2 вариант
   ;----------------------------------------------------
   ;$Result = CreateLinkLU ($KxlDir+"\Links.exe", $DestDirWork, $Name, $CommandFile, $Arg, $WorkDir, $IconFile,, $Name)
   ;----------------------------------------------------

   ;----------------------------------------------------
   ; 3 вариант
   ;----------------------------------------------------
   $Result = wshShortCut ($DestDirWork+"\"+$Name, $CommandFile, $Arg, $WorkDir, $IconFile, $WindowStyle, $Name)
   ;----------------------------------------------------
   if @Error <> 0
      LogAdd($Log,$LogFile,"I", "Ошибка при создании ("+$Name+"): "+@Error+" "+$SError) 
   else
      $ExeDir = $KxlDir+"\"
      ;----------------------------------------------------------------------
      ; PIF
      ;----------------------------------------------------------------------
      if Exist($FileNamePif)
         if Exist($PathAPP+"\"+$Name+".pif")
            LogAdd($Log,$LogFile,"I", $PathAPP+"\"+$Name+".pif"+" => "+$DestDirWork) 
            Copy $PathAPP+"\"+$Name+".pif" $DestDirWork
         else
            select
               case (@ProductType = "Windows 98") or (@ProductType = "Windows 95")
                  ;--------------------------------------------------------------
                  ; +0063h 0000h - default
                  ;        0010h - Закрывать окно по завершении работы
                  ;--------------------------------------------------------------
                  $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNamePif+'"'+" -o $63 -d $0010"
                  if $Debug 
                     LogAdd($Log,$LogFile,"I", $s) 
                  endif
                  shell "$s"
            EndSelect

            ;--------------------------------------------------------------
            ; 01ADh 00 02 10 02h - default
            ;                08h - полноэкранный режим
            ;          00        - обчный размер окна
            ;          10        - свернутое в значек
            ;          20        - развернутое во весь экран
            ;          80        - режим MS DOS        
            ;--------------------------------------------------------------
            if $WindowStyle = 3
               $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNamePif+'"'+" -o $1ad -d $00221008"
               if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               shell "$s"
            endif
         endif

      endif

      ;----------------------------------------------------------------------
      ; LNK
      ;----------------------------------------------------------------------
      if Exist($FileNameLnk)
         if Exist($PathAPP+"\"+$Name+".lnk")
            LogAdd($Log,$LogFile,"I", $PathAPP+"\"+$Name+".lnk"+" => "+$DestDirWork) 
            Copy $PathAPP+"\"+$Name+".lnk" $DestDirWork
         else
            if $WindowStyle = 3
               ;--------------------------------------------------------------
               ; Отображение во весь экран
               ;--------------------------------------------------------------
               if UCase($FileExt) = "EXE"
                  $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $3c -d $03"
                  if $Debug 
                     LogAdd($Log,$LogFile,"I", $s) 
                  endif
                  ; shell "$s"
               endif
               ;--------------------------------------------------------------
               ; Отображение во весь экран
               ;--------------------------------------------------------------
               if UCase($FileExt) = "BAT"
                  select
                     case $W2000
                        $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $234 -d $01"
                        shell "$s"
                     case $WXP or $WNT
                        $s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $12d -d $1c"
                        shell "$s"
                  endselect
                  ;--------------------------------------------------------------
                  ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $131 -d $2d"
                  ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
                  ;shell "$s"
                  ;--------------------------------------------------------------
               endif

               ;--------------------------------------------------------------
               ; Размер буфера экрана (высота)
               ;--------------------------------------------------------------
               ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $23f -d $19"
               ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               ;shell "$s"
               ;--------------------------------------------------------------
               ; Размер окна (высота)
               ;--------------------------------------------------------------
               ;$s = $ExeDir+"ChangeFile.exe -f "+'"'+$FileNameLnk+'"'+" -o $243 -d $19"
               ;if $Debug LogAdd($Log,$LogFile,"I", $s) endif
               ;shell "$s"
               ;--------------------------------------------------------------
            endif
         endif
      endif
   endif

   ;----------------------------------------------------
   $s = "      "
   if Exist($FileNamePif)
      ;$s = $s + $DestSubDir+"\"+GetFileName($FileNamePif)
      $s = $s + GetFileName($FileNamePif)
   else
      ;$s = $s + $DestSubDir+"\"+GetFileName($FileNameLnk)
      $s = $s + GetFileName($FileNameLnk)
   endif
   $s = $s + " ("+$WorkDir+")"
   if $Debug 
      LogAdd($Log,$LogFile,"I",$s)
   else
      LogAdd(10,  $LogFile,"I",$s)
   endif
   ;----------------------------------------------------
EndFunction

;------------------------------------------------------
; DeleteOne
;------------------------------------------------------
function DeleteOne ($Name, $DestDirWork, $DestSubDir)
   Dim $s,$FileNamePif,$FileNameLnk
;begin
   $FileNamePif = $DestDirWork+"\"+$Name+".pif"
   $FileNameLnk = $DestDirWork+"\"+$Name+".lnk"
   $s = "Delete link ... "
   if Exist($FileNamePif)
      $s = $s + $DestSubDir+"\"+GetFileName($FileNamePif)
      Del "$FileNamePif"
      LogAdd($Log,$LogFile,"I",$s)
   endif
   if Exist($FileNameLnk)
      $s = $s + $DestSubDir+"\"+GetFileName($FileNameLnk)
      Del "$FileNameLnk"
      LogAdd($Log,$LogFile,"I",$s)
   endif
EndFunction

;------------------------------------------------------
; GetDirWork
;------------------------------------------------------
function GetDirWork ($Dir, $SubDirAPP, optional $Server)
;begin
   select 
      case UCase($Dir) = UCase($FPrograms)
         if $Server
            $GetDirWork  = $Desktop+"\"+$sPrograms+"_"+$Server
         else
            $GetDirWork  = $Desktop+"\"+$sPrograms
         endif
      case UCase($Dir) = UCase($FTasks)
         if $Server
            $GetDirWork  = $Desktop+"\"+$sTasks+"_"+$Server
         else
            $GetDirWork  = $Desktop+"\"+$sTasks
         endif
      case UCase($Dir) = "DESKTOP"
         $GetDirWork  = $Desktop
      case UCase($Dir) = "PROGRAMS"
         $GetDirWork  = $Programs
      case UCase($Dir) = "STARTMENU"
         $GetDirWork  = $StartMenu
      case 1
         if Exist($Dir)
            $GetDirWork = $Dir
         else
            if $Server
               $GetDirWork  = $Desktop+"\"+$sPrograms+"_"+$Server
            else
               $GetDirWork  = $Desktop+"\"+$sPrograms
            endif
         endif
   endselect

   if $SubDirAPP <> ""
      $GetDirWork  = $GetDirWork+"\"+$SubDirAPP
   endif

EndFunction

;------------------------------------------------------
; CreateLinksGroup
;------------------------------------------------------
function CreateLinksGroup ($FileIni, $Group, $APP, optional $Server)
   Dim $Commands, $Command, $Server_APP
   Dim $s, $i
   Dim $Name
   Dim $DestDir, $DestDirWork, $SubDirAPP, $DestSubDir
   Dim $IconIndex
   Dim $Minimize, $Replace, $RunInOwnSpace
;begin 
   $Server_APP = ExtractWord(2, $APP, "_")

   $Commands = ReadSection($FileIni, $Group)
   $PathAPP = GetFilePath($FileIni)
   $i = 0
   for each $Command in $Commands
      $i = $i + 1
      $Name = $Command
      $s = ReadString ($FileIni, $Group, $Name, "")+"|"

      if $Debug 
         LogAdd($Log,$LogFile,"I", $Group+"="+$s) 
      endif

      $CommandFile   = ExtractWord(1, $s, "|")
      $Arg           = ExtractWord(2, $s, "|")
      $WorkDir       = ExtractWord(3, $s, "|")
      $IconFile      = ExtractWord(4, $s, "|")
      $IconIndex     = 0
      $DestDir       = ExtractWord(5, $s, "|") 
      $SubDirAPP     = ExtractWord(6, $s, "|")
      $WindowStyle   = ExtractWord(7, $s, "|")

      $DestDirWork   = GetDirWork($DestDir, $SubDirAPP, $Server)

      $DestSubDir    = $DestDirWork

      if $Debug 
         LogAdd($Log,$LogFile,"I", $DestDir+"|"+$SubDirAPP+"|"+$DestDirWork)
      endif

      if Exist ($CommandFile)
         if $Debug 
            LogAdd($Log,$LogFile,"I", "   "+$Name)
         endif

         ;----------------------------------------------------------------
         if (UCase($DestDir) = UCase($FPrograms)) and (not IsTerminalApplication)
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         else
         ;----------------------------------------------------------------
         if (UCase($DestDir) = UCase($FTasks)) and (IsTerminalApplication)
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         else
         if (UCase($DestDir) <> UCase($FPrograms)) and (UCase($DestDir) <> UCase($FTasks))
            CreateOne ($CommandFile, $Arg, $WorkDir, $IconFile, $Name, $DestDirWork, $DestSubDir, $WindowStyle, $PathAPP)
         endif
         endif
         endif
         ;----------------------------------------------------------------

      else
         if $CommandFile <> ""
            if UCase($APP) <> UCase("Terminal")
               LogAdd($Log,$LogFile,"I", "ERROR: "+$Name+" Файл "+$CommandFile+" не существует или нет доступа")
            endif
         endif

         ; Delete Lnk

         if UCase($DestDir) = UCase($FPrograms) and (not IsTerminalApplication)
            if $SubDirAPP <> ""
               $DestSubDir =  $sPrograms+"\"+$SubDirAPP
            else
               $DestSubDir =  $sPrograms
            endif
            DeleteOne ($Name, $DestDirWork, $DestSubDir)
         else

         if UCase($DestDir) = UCase($FTasks) and (IsTerminalApplication)
            if $SubDirAPP <> ""
               $DestSubDir =  $sTasks+"\"+$SubDirAPP
            else
               $DestSubDir =  $sTasks
            endif
            DeleteOne ($Name, $DestDirWork, $DestSubDir)
         endif
         endif
      endif
   next
EndFunction

;------------------------------------------------------
; CreateLinksAPP
;------------------------------------------------------
function CreateLinksAPP ($FileIni, $Domain, $Group, optional $Server)
   Dim $FileINIApp, $LGroup, $i, $j, $n, $Done, $Object, $Objects, $s, $LGroups, $Item
;begin

   $FileINIApp = ReadString ($FileINI, $Domain, $Group, "")

   if Exist($FileINIApp) 

      if $Debug
         LogAdd($Log,$LogFile,"I","Файл "+$FileINIApp+" существует")
      endif

      $Objects = ReadSections($FileINIAPP)
      for each $Object in $Objects
         $j = 0
         if $Object <> ""
            if $Debug
               LogAdd($Log,$LogFile,"I",$Object)
            endif

            $n = WordCount($Object, ",")
            if $n > 0

               ;------------------------------------------------------
               ; реализация ИЛИ
               ;------------------------------------------------------
               $Done = 0
               $i = 1

               while ($i <= $n) and ($Done = 0)
                  $Item = ExtractWord($i, $Object, ",")
                  if $Item 

                     ;------------------------------------------
                     ; User без домена
                     ;------------------------------------------
                     if WordCount($Item, "\") = 1
                        $Item = @LDomain+"\"+$Item+"\"
                     endif

                     $LDomen = ExtractWord(1, $Item, "\")
                     $s = ExtractWord(2, $Item, "\")

                     ;------------------------------------------
                     ; User
                     ;------------------------------------------
                     if WordCount($Item, "\") = 3
                        if $Debug
                           LogAdd($Log,$LogFile,"I","USER="+$Item)
                           LogAdd($Log,$LogFile,"I",UCase($LDomen+"\"+$s)+"="+UCase($DomainUser+"\"+$UserID))
                        endif

                        ;------------------------------------------------
                        ;if UCase($s) = UCase($UserID)
                        ;   $Done = 1
                        ;endif
                        ;------------------------------------------------

                        if UCase($LDomen+"\"+$s) = UCase($DomainUser+"\"+$UserID)
                           $Done = 1
                        endif

                     else
                        if $Debug
                           LogAdd($Log,$LogFile,"I","GROUP="+$Item)
                        endif
                        ;------------------------------------------
                        ; GROUP
                        ;------------------------------------------
                        if (UCase($DomainUser) = "GU"    and UCase($LDomen) = "DSP") or
                           (UCase($DomainUser) = "DSP"   and UCase($LDomen) = "GU") or
                           (UCase($DomainUser) = "ULNSK" and UCase($LDomen) = "DSP") or
                           (UCase($DomainUser) = "ULNSK" and UCase($LDomen) = "GU") 
                           if $Debug
                              LogAdd($Log,$LogFile,"I","Нет проверки вхождения в группу "+$Item+". ("+
                                 $DomainUser+"="+$LDomen+")")
                           endif
                        else

                           ;If InGroup ($Item)

                           ;If (IndexOf($arrAllGroupsUser, $Item) >= 0)

                           if (IndexOf($FUserAllGroups, $Item) >= 0)

                              if $Debug
                                 LogAdd($Log,$LogFile,"I","*  "+$Item)
                              endif
                              $Done = 1
                           else
                              ;-----------------------------------------------------
                              ;if IndexOf($FUserAllGroups, $Item) >= 0
                              ;-----------------------------------------------------
                              if IndexOf($arrAllGroupsUser, $Item) >= 0
                                 if $Debug
                                    LogAdd($Log,$LogFile,"I","** "+$Item)
                                 endif
                                 $Done = 1
                              endif
                              if $Debug
                                 LogAdd($Log,$LogFile,"I","************** "+$Item)
                              endif
                           endif
                        endif
                     endif
                  endif
                  $i = $i + 1
               loop

               ;------------------------------------------------------
               if $Done = 1
                  $DomainAPP = ExtractWord(1, $Item, "\")
                  if $Debug
                     LogAdd($Log,$LogFile,"I","DomainAPP="+UCase($DomainAPP)+" "+
                                              "DomainUser="+UCase($DomainUser)+" "+
                                              "DomainPC="+UCase($DomainPC))
                  endif
                  select
                     case UCase($DomainAPP) = UCase($DomainDSP)
                        if (UCase($DomainUser) = UCase($DomainDSP)) and (UCase($DomainPC) = UCase($DomainDSP))
                           $j = $j + 1         
                           CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                        else
                        ;-------------------------------------------------------------
                        if (UCase($DomainUser) = UCase($DomainDSP)) and (UCase($DomainPC) <> UCase($DomainDSP))
                           $j = $j + 1         
                           CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                        endif
                        ;-------------------------------------------------------------
                        endif
                     case 1
                        $j = $j + 1         
                        CreateLinksGroup ($FileINIApp, $Object, $Group, $Server)
                  endselect
               endif
               ;------------------------------------------------------

            endif
         endif
      next
   else
      if $Debug
         LogAdd($Log,$LogFile,"I","Файл "+$FileINIApp+" не существует или нет доступа")
      endif
   endif
EndFunction

;------------------------------------------------------
; CreateLinks0
;------------------------------------------------------
function CreateLinks0 ($FileIni)
   Dim $Domains, $Groups, $Group, $Domain, $i, $DomainGroup
;begin 
   $Groups = AllGroups(0)
   for each $DomainGroup in $Groups
      $i = WordCount($DomainGroup, "\")
      if $i > 1
         $Domain = ExtractWord(1, $DomainGroup, "\")
         $Group = ExtractWord(2, $DomainGroup, "\")
         if ValueExists ($FileIni, $Domain, $Group) = True
            $DomainAPP = $Domain
            CreateLinksAPP ($FileIni, $Domain, $Group)
         endif
      endif
   next
EndFunction

;------------------------------------------------------
; CreateLinks3
;------------------------------------------------------
function CreateLinks3 ($FileIni, optional $Server)
   Dim $Domains, $APPs, $APP
;begin 
   LogAdd ($Log, $LogFile, "I", "========================================")
   LogAdd ($Log, $LogFile, "I", "FileINI_APP = " + $FileIni)
   $Domains = ReadSections($FileINI)
   for each $Domain in $Domains
      $DomainAPP = $Domain
      if $Domain <> ""
         LogAdd($Log,$LogFile,"I","=====================================")
         LogAdd($Log,$LogFile,"I","Домен: "+$DomainAPP)
         $APPs = ReadSection($FileINI, $Domain)
         for each $APP in $APPs
            if $APP <> ""
               LogAdd($Log,$LogFile,"I","-------------------------------------")
               LogAdd($Log,$LogFile,"I","Программа: "+$APP)
               CreateLinksAPP ($FileIni, $Domain, $APP, $Server)
            endif
         next
      endif
   next
   LogAdd ($Log, $LogFile, "I", "========================================")
EndFunction

;------------------------------------------------------
; ClearDir
;------------------------------------------------------
function ClearDir ($Dir, optional $Server)
   Dim $s
;begin
   $s = GetDirWork($Dir, "", $Server)
   LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
   DelDir ($s, $Log, $LogFile)
   RD ($s)

   ;----------------------------------------------------------
   ; Start Menu
   ;----------------------------------------------------------
   select
      case UCase($Dir) = UCase($FPrograms)
         if $Server
            $s = $Programs+"\"+$sPrograms+"_"+$Server
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         else
            $s = $Programs+"\"+$sPrograms
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir ($s, $Log, $LogFile)
         endif
      case UCase($Dir) = UCase($FTasks)
         if $Server
            $s = $Programs+"\"+$sTasks+"_"+$Server
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir($s, $Log, $LogFile)
         else
            $s = $Programs+"\"+$sTasks
            LogAdd($Log,$LogFile,"I", "Удаление ... "+$s)
            DelDir($s, $Log, $LogFile)
         endif
   endselect
EndFunction

;------------------------------------------------------
; CreateLinks
;------------------------------------------------------
function CreateLinks
;begin
   $Log = 30 
   if @LDomain = ""
      $LogDir = $LogPath+"\logon"
   else
      $LogDir = $LogPath+"\logon\"+@LDomain
   endif
   if not Exist($LogDir) MD $LogDir endif
   $s = AddCharR("_",$USERID,8)
   $LogFile = $s+"_"+UCase(@WKSTA)+".lnk"
   $LogFile = LogFileName($Log,$LogDir,$LogFile)

   if $Debug
       LogAdd ($Log, $LogFile, "I", "==================================================")
       LogAdd ($Log, $LogFile, "I", "Режим отладки включен.")
   endif
   ;------------------------------------------------------
   $FUserAllGroups = AllGroups
   $arrAllGroupsUser = GetAllGroupsUser(0)
   ;------------------------------------------------------

   ;------------------------------------------------------
   $DomainUser = @LDomain
   $DomainPC   = @Domain
   $PCUser     = %COMPUTERNAME%
   if $PCUser = "" $PCUser = @Wksta endif
   ;------------------------------------------------------

   ;------------------------------------------------------
   ;
   ;------------------------------------------------------
   LogonLog  ($LogPath+"\logon")
   if $Debug
      TitleInfo (30, $LogFile)
   else
      TitleInfo (10, $LogFile)
   endif

   ;------------------------------------------------------
   ; Backup Kix32
   ;------------------------------------------------------
   if @UserID = "luis"
      $SystemDir = %WINDIR%
      $s = $SystemDir+"\"+"kx*.*"
      LogAdd ($Log, $LogFile, "I", "Удаление "+$s)
      Del $S /f
      $s = $SystemDir+"\"+"kix*.*"
      LogAdd ($Log, $LogFile, "I", "Удаление "+$s)
      Del $S /f
      $s = $SystemDir+"\"+"wkix*.*"
      LogAdd ($Log, $LogFile, "I", "Удаление "+$s)
      Del $S /f
      $SystemDir = %WINDIR%+"\kix"
      $KixDirKix = $KxlDir+"\KIX\KIX_4_53"
      SetPath($SystemDir)
   else
      $SystemDir = %WINDIR%
      $KixDirKix = $KxlDir+"\Kix"
   endif
   BacFile ($KixDirKix, $SystemDir, "kix32.exe", True)
   ;------------------------------------

   LogAdd ($Log, $LogFile, "I", "===========================================")
   LogAdd ($Log, $LogFile, "I", " Создание ярлыков ")
   LogAdd ($Log, $LogFile, "I", "===========================================")

   ClearDir($FPrograms)
   ClearDir($FTasks)

   ;------------------------------------------------------
   ;ClearDir($FPrograms,"S73FS01")
   ;ClearDir($FTasks,"S73FS01")
   ;------------------------------------------------------

   select
      case Ucase ("%USERDOMAIN%") = "REGION"
         CreateLinks3 ($FileINI_APP_REGION)
      case Ucase ("%USERDOMAIN%") = "ULNSK"
         CreateLinks3 ($FileINI_APP_ULNSK)
      case Ucase ("%USERDOMAIN%") = "GU"
         CreateLinks3 ($FileINI_APP_GU)
      case Ucase ("%USERDOMAIN%") = "DSP"
         CreateLinks3 ($FileINI_APP_DSP)
      case 1
         MapDrives ($FileINI_GU)
         CreateLinks3 ($FileINI_APP_GU)
   endselect

   $DestDirWork1 = GetDirWork($sPrograms, "")
   $DestDirWork2 = $Programs+"\"+$sPrograms
   LogAdd($Log,$LogFile,"I", $DestDirWork1+" => "+$DestDirWork2)
   BacFiles ($DestDirWork1, $DestDirWork2, "*.*", True)

   ;------------------------------------
   ;CreateLinks3 ($FileINI_APP_S73FS01, "S73FS01")
   ;------------------------------------
   ;------------------------------------
   ;$DestDirWork1 = GetDirWork($sPrograms, "", "S73FS01")
   ;$DestDirWork2 = $Programs+"\"+$sPrograms+"_"+"S73FS01"
   ;LogAdd($Log,$LogFile,"I", $DestDirWork1+" => "+$DestDirWork2)
   ;BacFiles ($DestDirWork1, $DestDirWork2, "*.*", True)
   ;------------------------------------
endfunction
