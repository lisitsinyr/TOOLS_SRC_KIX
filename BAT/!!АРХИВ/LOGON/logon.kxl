;=======================================================================================
; Logon.kxl
;=======================================================================================

;-----------------------------------------------------------
; SetIE
;-----------------------------------------------------------
function SetIE
;begin
   LogAdd ($Log, $LogFile, "I", "===========================================")
   LogAdd($Log,$LogFile,"I", "Установка начальных настроек для Internet Explorer"+"...")

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings"
   $Result = WriteValue ($Key, "ProxyOverride", "www.ulnsk.cbr.ru;ulnsk.cbr.ru;<local>", "REG_SZ")
   $Result = WriteValue ($Key, "ProxyServer",   "proxy.ulnsk.cbr.ru:3128", "REG_SZ")
   $Result = WriteValue ($Key, "ProxyEnable",   "0,0,0,0", "REG_BINARY")

   $Key = "HKCU\Software\Microsoft\Internet Explorer\Main"
   $Result = WriteValue ($Key, "Start Page", "http://73portal", "REG_SZ")

   ;----------------------------------------------------------------------
   ; от Минделя
   ;----------------------------------------------------------------------
   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s73sofpi.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\73portal.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s73wb03.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s73wb05.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s22portal.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\www2.portal"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\EscDomains\s73sofpi"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\EscDomains\73portal"
   RegWriteDWORD ($Key, "http", 1)
   ;----------------------------------------------------------------------
   ; от Яхонтова
   ;----------------------------------------------------------------------
   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s73-sou-web.region"
   RegWriteDWORD ($Key, "http", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s73-sou-sso.region"
   RegWriteDWORD ($Key, "https", 1)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s39websouits6.vip"
   RegWriteDWORD ($Key, "http", 2)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\s39ssosrv.vip"
   RegWriteDWORD ($Key, "https", 2)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\ZoneMap\Domains\cbr.ru\scapersonalapp.vip"
   RegWriteDWORD ($Key, "http", 2)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Internet Settings\Zones\2"
   RegWriteDWORD ($Key, "Flags", 67)

endfunction

;-----------------------------------------------------------
; SyncTime
;-----------------------------------------------------------
function SyncTime
;begin
   LogAdd ($Log, $LogFile, "I", "===========================================")
   LogAdd($Log,$LogFile,"I", "Синхронизация времени с " + @LServer+"...")
   SetTime "@LServer"
endfunction
                                                                                                                                                        
;-----------------------------------------------------------
; 
;-----------------------------------------------------------
function SetTen
;begin
   ;-----------------------------------------------------------
   ; Теневое копирование тома
   ;-----------------------------------------------------------
   Shell 'net stop "Теневое копирование тома"'
   $Key = "HKLM\SYSTEM\CurrentControlSet\Services\VSS"
   RegWriteDWORD ($Key, "Start", 4)
   $VSS = RegReadString ($Key, "Start")
   if $VSS
      LogAdd($Log,$LogFile,"I", "VSS:"+$VSS)
   else
      LogAdd($Log,$LogFile,"I", "VSS:0")
   endif
endfunction

;-----------------------------------------------------------
; MapDrives
;-----------------------------------------------------------
function MapDrives ($FileINI)
   Dim $Done,$i
;begin
   LogAdd ($Log, $LogFile, "I", "===========================================")
   LogAdd($Log,$LogFile,"I", "Подключение сетевых дисков для "+$UserID+"...")
   if not EXIST ($FileINI)
      LogAdd ($Log, $LogFile, "I", "ERROR: Файл ini "+$FileINI+" отсутствует")
   else
      LogAdd ($Log, $LogFile, "I", $FileINI)
      ;------------------------------------------------------------------------
      $arrAllGroupsUser = GetAllGroupsUser(0)
      ;$FUserAllGroups = AllGroups
      ;------------------------------------------------------------------------
      $Objects = ReadSections($FileINI)
      for each $Object in $Objects
         $j = 0
         if $Object 
            LogAdd($Log,$LogFile,"I", "["+$Object+"]")
            $n = WordCount($Object, ",")
            if $n > 0
               $Done = 0
               $DoneSTD = 0
               $i = 1
               while ($i <= $n) and ($Done = 0)
                  $Item = ExtractWord($i, $Object, ",")
                  if $Item 
                     ;------------------------------------------
                     ; User без домена
                     ;------------------------------------------
                     if WordCount($Item, "\") = 1
                        $Item = UCase(@LDomain)+"\"+$Item+"\"
                     endif
                     $LDomen = ExtractWord(1, $Item, "\")
                     $s = ExtractWord(2, $Item, "\")
                     if (UCase($s) = UCase($UserID)) or (UCase($s) = UCase("All"))
                        $Done = 1
                     else
                        if (UCase($s) = UCase($UserID)) or (UCase($s) = UCase("STD"))
                           if IsTerminalApplication
                              $DoneSTD = 1
                           endif
                        else
                           if WordCount($Item, "\") = 3
                              ;-----------------------------------------------------------
                              ;LogAdd($Log,$LogFile,"I","Это пользователь = "+$Item)
                              ;-----------------------------------------------------------
                           else
                              ;-----------------------------------------------------------
                              ;LogAdd($Log,$LogFile,"I","Это группа = "+$Item)
                              ;-----------------------------------------------------------
                              If (IndexOf($arrAllGroupsUser, $Item) >= 0)
                                 $Done = 1
                              else
                                 if IndexOf($FUserAllGroups, $Item) >= 0
                                    $Done = 1
                                 endif
                              endif
                           endif
                        endif
                     endif
                  endif
                  $i = $i + 1
               loop
               ;------------------------------------------------------
               if $Done = 1
                  $Drives = ReadSection($FileINI, $Object)
                  for each $Drive in $Drives
                     $s = ReadString ($FileIni, $Object, $Drive, "")+"|"
                     $Path = ExtractWord(1, $s, "|")
                     $Persistent = ExtractWord(2, $s, "|")
                     ;LogAdd($Log,$LogFile,"I", $Drive+" => "+$Path)
                     MapDrive (Substr($Drive,1,1), $Path, $LogFile, $Persistent)
                  next
               endif
               if $DoneSTD = 1
                  $Drives = ReadSection($FileINI, $Object)
                  for each $Drive in $Drives
                     $s = ReadString ($FileIni, $Object, $Drive, "")+"|"
                     $Path = ExtractWord(1, $s, "|")
                     $Persistent = ExtractWord(2, $s, "|")
                     ;LogAdd($Log,$LogFile,"I", $Drive+" => "+$Path)
                     MapDrive (Substr($Drive,1,1), $Path, $LogFile, $Persistent)
                  next
               endif
               ;------------------------------------------------------
            endif
         endif
      next
   endif
endfunction

;-----------------------------------------------------------
; Установка начальных настроек для CITRIX для НАШЕЙ открытой фермы
;-----------------------------------------------------------
;-----------------------------------------------------------
; SetCITRIX_REGION
;-----------------------------------------------------------
function SetCITRIX_REGION()
;begin
   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\Shell Folders"
   $KeyValue ="\\S73fs03\std_info\HOMES\"+@UserID+"\Documents"
   $Result = WriteValue ($Key, "Personal", $KeyValue, "REG_SZ")
   LogAdd ($Log, $LogFile, "I", $Key)
   LogAdd ($Log, $LogFile, "I", "Personal="+$KeyValue)

   $Key = "HKCU\Software\Microsoft\Windows\CurrentVersion\Explorer\User Shell Folders"
   $KeyValue ="\\S73fs03\std_info\HOMES\"+@UserID+"\Documents"
   $Result = WriteValue ($Key, "Personal", $KeyValue, "REG_EXPAND_SZ")
   LogAdd ($Log, $LogFile, "I", $Key)
   LogAdd ($Log, $LogFile, "I", "Personal="+$KeyValue)
endfunction

;-----------------------------------------------------------
; WriteGGU
;-----------------------------------------------------------
function WriteGGU()
;begin

   $OutFileName = $LogPath_S73FS01+"\logon\GGU\"+AddCharR("_",@LDomain,6)+"_"+@UserID+".txt"
   $DomainUser_01 = GET_DOMAIN_USER ($IPAddr)
   if @LDomain <> $DomainUser_01
      $OutFileName = $LogPath_S73FS01+"\logon\GGU\"+AddCharR("_","DSPESK",6)+"_"+@UserID+".txt"
   endif

   $arrGlobalGroups = GetAllGroupsUser(0)
   DEL $OutFileName
   $HandleOut = FreeFileHandle
   $Res = Open ($HandleOut, $OutFileName, 1+4)
   ;$Res = WriteLine ($HandleOUT, $IPAddr+@CRLF)
   For Each $GlobalGroup In $arrGlobalGroups
      $Res = WriteLine ($HandleOUT, $GlobalGroup+@CRLF)
   next
   $Res = Close ($HandleOut)

endfunction
